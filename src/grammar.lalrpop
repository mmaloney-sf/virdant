use crate::common::*;
use crate::ast::*;

grammar;

pub Package: Package = {
    <items: Item*> => Package {
      items,
    },
}

Item: Item = {
    <m:ModDef> => Item::ModDef(m),
    <e:StructTypeDef> => Item::StructTypeDef(e),
    <e:AltTypeDef> => Item::AltTypeDef(e),
}

Visibility: Visibility = {
  <v:"pub"?> => {
    if v.is_none() {
      Visibility::Private
    } else {
      Visibility::Public
    }
  }
}

ModDef: ModDef = {
    <v:Visibility> "mod" <name:Id> "{"
        <decls:(Decl ";")*>
    "}" => {
      let decls = decls.into_iter().map(|(decl, _semi)| decl).collect();
      ModDef {
        name,
        decls,
      }
    },
}

StructTypeDef: StructTypeDef = {
    "struct" "type" <name:Id> "{"
        <fields:(Id ":" Type ";")*>
    "}" => {
        StructTypeDef {
            name,
            fields: fields.iter().map(|(name, _, typ, _)| (name.clone(), typ.clone())).collect::<Vec<_>>(),
        }
    },
}

AltTypeDef: AltTypeDef = {
    <ll:@L> <vis:Visibility> "alt" "type" <name:Id> "{"
        <alts:AltTypeDefAlt*>
    "}" <rr:@R> => {
        AltTypeDef {
            name,
            alts,
        }
    },
}

AltTypeDefAlt: (Ident, Vec<Arc<Type>>) = {
    <ident:Id> "(" <typs:TypeList> ")" ";" => (ident, typs),
}

TypeList: Vec<Arc<Type>> = {
    <args:((Type ("," Type)* ","?)?)> => {
        if let Some((typ, args, _comma)) = args {
            let mut results = vec![typ];
            for (_comma, typ) in args {
                results.push(typ);
            }
            results
        } else {
            vec![]
        }
    },
}

Decl: Decl = {
    <c:SimpleComponent> => Decl::SimpleComponent(c),
    <c:Wire> => Decl::Wire(c),
    <m:Submodule> => Decl::Submodule(m),
}

SimpleComponent: SimpleComponent = {
    "incoming" <name:Id> ":" <typ:Type> => {
        SimpleComponent {
            kind: SimpleComponentKind::Incoming,
            name,
            typ: typ,
            clock: None,
            reset: None,
        }
    },
    "outgoing" <name:Id> ":" <typ:Type> => {
        SimpleComponent {
            kind: SimpleComponentKind::Outgoing,
            name,
            typ: typ,
            clock: None,
            reset: None,
        }
    },
    "node" <name:Id> ":" <typ:Type> => {
        SimpleComponent {
            kind: SimpleComponentKind::Node,
            name,
            typ: typ,
            clock: None,
            reset: None,
        }
    },
    "reg" <name:Id> ":" <typ:Type> "on" <clock:Path> => {
//        let reset = rst.map(|s| s.1); // .1 ignores the "reset" keyword
        SimpleComponent {
            kind: SimpleComponentKind::Reg,
            name,
            typ,
            clock: Some(clock),
            reset: None,
        }
    },
}

Wire: Wire = {
    <target:Path> ":=" <e:Expr> => Wire(target, WireType::Continuous, e),
    <target:Path> "<=" <e:Expr> => Wire(target, WireType::Latched, e),
}

Submodule: Submodule = {
    "mod" <name:Id> "of" <moddef:Id> => Submodule { name, moddef },
}

Type: Arc<Type> = {
    "Clock" => Arc::new(Type::Clock),
    "Word" "[" <w:Nat> "]" => Arc::new(Type::Word(w)),
    <id:Id> => Arc::new(Type::TypeRef(id)),
}

pub Expr: Arc<Expr> = {
//    <e:ExprLet> => e,
//    <e:ExprMatch> => e,
    <e:ExprIf> => e,
    <e:ExprCall> => e,
}

ExprIf: Arc<Expr> = {
    <ll:@L> "if" <cond:Expr> "{"
        <e1:Expr>
    <elseifs:("}" "else" "if" Expr "{" Expr)*>
    "}" "else" "{"
        <elseexpr:Expr>
    "}" <rr:@R> => {
        let mut result = elseexpr;

        for (_, _, _, elseif_cond, _, elseif_body) in elseifs.into_iter().rev() {
            result = Arc::new(Expr::If(elseif_cond, elseif_body, result));
        }

        Arc::new(Expr::If(cond, e1, result))
    },
}

ExprCall: Arc<Expr> = {
//    <ll:@L> <name:Ctor> <rr:@R> =>  Arc::new(Expr::Call(name, vec![], vec![])),
    <ll:@L> <name:Ctor> "(" <es:ExprList> ")" <rr:@R> =>  Arc::new(Expr::Ctor(name, es)),                            

    "cat" "(" <es:ExprList> ")" => {
        Arc::new(Expr::Cat(es))
    },

//    <ll:@L> <name:Id> "(" <es:ExprList> ")" <rr:@R> => {
//        Arc::new(Expr::FnCall(name, es))
//    },
    <ll:@L> <s:ExprCall> "->" <name:Id> "(" <es:ExprList> ")" <rr:@R> => {
        Arc::new(Expr::MethodCall(s, name, es))
    },
    <ll:@L> <s:ExprCall> "->" "as" "(" <typ:Type> ")" <rr:@R> => {
        Arc::new(Expr::As(s, typ))
    },
    <e:ExprIdx> => e,
}

ExprIdx: Arc<Expr> = {
//    <ll:@L> <e:ExprIdx> "->" <field:Id> <rr:@R> => Arc::new(Expr::IdxField(e, field.clone())),
    <e:ExprIdx> "[" <i:Nat> "]" => Arc::new(Expr::Idx(e, i)),
    <ll:@L> <e:ExprIdx> "[" <j:Nat> ".." <i:Nat> "]" <rr:@R> => Arc::new(Expr::IdxRange(e, j, i)),
    <e:ExprBase> => e,
}

ExprBase: Arc<Expr> = {
    <e:ExprVec> => e,
//    <e:ExprStruct> => e,
//    <e:ExprWith> => e,
//    <e:ExprAs> => e,
    <e:ExprLit> => e,
    <e:ExprReference> => e,
    "(" <e:Expr> ")" => e,
}

FieldExprList: Vec<(String, Arc<Expr>)> = {
    <fields:((Id "=" Expr ("," Id "=" Expr)* ","?)?)> => {
        if let Some((name, _eqsign, expr, fields, _comma)) = fields {
            let mut results = vec![(name.to_string(), expr)];
            for (_comma, name, _eqsign, expr) in fields {
                results.push((name.to_string(), expr));
            }
            results
        } else {
            vec![]
        }
    },
}

/*
ExprWith: Arc<Expr> = {
  "with" <e:Expr> "{"
    <edits:ExprWidthEdit*>
  "}" => {
    Arc::new(Expr::With(e, edits))
  }
}

ExprWidthEdit: WithEdit = {
  "this" "[" <n:Nat> "]" "=" <e:Expr> ";" => WithEdit::Idx(n, e),
  "this" "->" <id:Id> "]" "=" <e:Expr> ";" => WithEdit::Field(id, e),
}
*/

ExprVec: Arc<Expr> = {
    <ll:@L> "[" <es:ExprList> "]" <rr:@R> => Arc::new(Expr::Vec(es)),
    <ll:@L> "[" <e:Expr> ";" <n:Nat> "]" <rr:@R> => Arc::new(Expr::Vec(vec![e; n as usize])),
}

ExprList: Vec<Arc<Expr>> = {
    <exprs:((Expr ("," Expr)* ","?)?)> => {
        if let Some((expr, exprs, _comma)) = exprs {
            let mut results = vec![expr];
            for (_comma, expr) in exprs {
                results.push(expr);
            }
            results
        } else {
            vec![]
        }
    },
}

ExprLit: Arc<Expr> = {
    <ll:@L> <lit:WordLit> <rr:@R> => {
        Arc::new(Expr::Word(lit))
    },

//    "true" => Arc::new(Expr::Bool(true)),
//    "false" => Arc::new(Expr::Bool(false)),
}

ExprReference: Arc<Expr> = {
  <path: Path> => Arc::new(Expr::Reference(path))
}


Path: Path = {
    <id0:Id> <ids:("." Id)*> => {
        let mut parts = vec![id0.to_string()];
        for (_, id) in ids {
          parts.push(id.to_string());
        }
        let path: String = parts.join(".").chars().collect();
        path.into()
    },
}

WordLit: WordLit = {
    <n:Nat> => {
        WordLit {
            value: n,
            width: None,
        }
    },
    <lit:r"[0-9][_0-9]*w[0-9]+"> => {
        let lit_nounders: String = lit.chars().filter(|&c| c != '_').collect();
        let parts = lit_nounders.split("w").collect::<Vec<_>>();
        let v = parts[0].parse().unwrap();
        let width = parts[1].parse().unwrap();
        WordLit {
            value: v,
            width: Some(width),
        }
    },
    <lit:r"0b[0-1][_0-1]*w[0-9]+"> => {
        let lit_nounders: String = lit.chars().filter(|&c| c != '_').collect();
        let parts = lit_nounders.split("w").collect::<Vec<_>>();
        let v = u64::from_str_radix(&parts[0][2..], 2).unwrap();
        let width = parts[1].parse().unwrap();
        WordLit {
            value: v,
            width: Some(width),
        }
    },
    <lit:r"0x[0-9a-fA-F][_0-9a-fA-F]*w[0-9]+"> => {
        let lit_nounders: String = lit.chars().filter(|&c| c != '_').collect();
        let parts = lit_nounders.split("w").collect::<Vec<_>>();
        let v = u64::from_str_radix(&parts[0][2..], 16).unwrap();
        let width = parts[1].parse().unwrap();
        WordLit {
            value: v,
            width: Some(width),
        }
    },
}

Nat: u64 = {
    <lit:r"[0-9][_0-9]*"> => {
        let lit_nounders: String = lit.chars().filter(|&c| c != '_').collect();
        lit_nounders.parse().unwrap()
    },
    <lit:r"0b[0-9][_0-9]*"> => {
        let lit_nounders: String = lit.chars().filter(|&c| c != '_').collect();
        u64::from_str_radix(&lit_nounders[2..], 2).unwrap()
    },
    <lit:r"0x[0-9][_0-9]*"> => {
        let lit_nounders: String = lit.chars().filter(|&c| c != '_').collect();
        u64::from_str_radix(&lit_nounders[2..], 16).unwrap()
    },
}

Ctor: Ident = {
    <rr:@R> <id:r"@[_A-Za-z][_A-Za-z0-9]*"> <ll:@L> => {
        id[1..].into()
    },
}

Id: Ident = {
    <ll:@L> <id:r"[_A-Za-z][_A-Za-z0-9]*"> <rr:@R> => {
        id.into()
    },
}

match {
    r"//[^\n\r]*[\r\n]" => {},
    r"[\r\n]*" => {},
    r"/\*[^*]*\*/" => {},
    r" " => {},
    _,
}
